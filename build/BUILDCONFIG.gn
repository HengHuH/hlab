# This file is part of the GN build system for hlab.

if (target_os == "") {
  target_os = host_os
}

if (target_cpu == "") {
  target_cpu = host_cpu
}

if (current_cpu == "") {
  current_cpu = target_cpu
}

if (current_os == "") {
  current_os = target_os
}

# =============================================================================
# BUILD FLAGS
# =============================================================================

declare_args() {
  # Set to enable the official build level of optimization.
  # IMPORTANT NOTE: (!is_debug) is *not* sufficient to get satisfying.
  # set "is_official_build" to true for any build intended to ship to end-users.
  is_official_build = false

  custom_toolchain = ""

  # This should not normally be set as a build argument.
  host_toolchain = ""
}

declare_args() {
  is_debug = !is_official_build
}

assert(!(is_debug && is_official_build), "Can't do official debug builds")

# =============================================================================
# TOOLCHAIN SETUP
# =============================================================================

if (host_toolchain == "") {
  if (host_os == "win") {
    host_toolchain = "//build/toolchain/win:$host_cpu"
  } else {
    assert(false, "Unsupported host_os: $host_os")
  }
}

_default_toolchain = ""

if (target_os == "win") {
  _default_toolchain = "//build/toolchain/win:$target_cpu"
}

if (custom_toolchain != "") {
  set_default_toolchain(custom_toolchain)
} else if (_default_toolchain != "") {
  set_default_toolchain(_default_toolchain)
}

# =============================================================================
# OS DEFINITIONS
# =============================================================================

is_win = current_os == "win"
is_linux = current_os == "linux"
is_android = current_os == "android"
is_ios = current_os == "ios"
is_mac = current_os == "mac"

is_apple = is_ios || is_mac
is_posix = !is_win

# =============================================================================
# OS DEFINITIONS
# =============================================================================

# Holds all configs used for running the compiler.
default_compiler_configs = [
  "//build/config:compiler",
  "//build/config:default_optimization",
  "//build/config:default_symbols",
  "//build/config:language",
  "//build/config:rtti",
  "//build/config:exceptions",
  "//build/config:runtime_library",
]

# Debug/Release related defines.
if (is_debug) {
  default_compiler_configs += [ "//build//config:debug" ]
} else {
  default_compiler_configs += [ "//build//config:release" ]
}

# OS dependency
if (is_win) {
  default_compiler_configs += [
    "//build/config/win:utf8",
    "//build/config/win:default_crt",
    "//build/config/win:lean_and_mean",
    "//build/config/win:nominmax",
    "//build/config/win:unicode",
    "//build/config/win:winver",
  ]
} else {
  default_compiler_configs += []
}

# Static libraries and source sets use only the compiler configs.
set_defaults("static_library") {
  configs = default_compiler_configs
}

set_defaults("source_set") {
  configs = default_compiler_configs
}

# Link common configs
if (is_win) {
  _linker_configs = [
    "//build/config/win:console"
  ]
} else {
  _linker_configs = []
}


# Executable defaults.
set_defaults("executable") {
  configs = default_compiler_configs + [
              "//build/config:default_libs",
              "//build/config:executable_config",
            ] + _linker_configs
}

# Shared library and loadable module defaults.
default_shared_library_configs = default_compiler_configs + [
                                   "//build/config/default_libs",
                                   "//build/config/shared_library_config",
                                 ] + _linker_configs

set_defaults("shared_library") {
  configs = default_shared_library_configs
}
