# This file is part of the GN build system for hlab.

declare_args() {
  # Path to Visual Studio.
  # If set, you must also set the visual_studio_version and msvc_version.
  visual_studio_path = ""

  # Version of Visual Studio pointed to by the visual_studio_path.
  visual_studio_version = ""

  # Version of MSVC, default is newest installed version in vs version.
  msvc_version = ""

  # Full path to the Windows SDK, not including a backslash at the end.
  # This value is the default location, override if you have a different
  # installation location.
  windows_sdk_path = "C:/Program Files (x86)/Windows Kits/10"

  # Version of windows sdk, specifying 10.0.19041.0.
  windows_sdk_version = "10.0.19041.0"
}

if (visual_studio_path == "") {
  # Auto detect newest version.
  toolchain_data = exec_script("toolchain.py", ["auto_detect_toolchain"], "scope")

  visual_studio_path = toolchain_data.vs_path
  visual_studio_version = toolchain_data.vs_version
  msvc_version = toolchain_data.msvc_version

  visual_studio_runtime_dirs = toolchain_data.runtime_dirs
} else {
  assert(visual_studio_version != "", "You must set the visual_studio_version if you set the path.")
  assert(msvc_version != "", "You must set the msvc_version if you set the path.")

  visual_studio_runtime_dirs = []
}
